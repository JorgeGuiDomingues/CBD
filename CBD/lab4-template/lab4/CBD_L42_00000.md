// NMEC: 113278

## 1 Encontre todos os atores que dirigiram um filme em que também atuaram e apresente o nome do ator e o título do filme.
```sql
MATCH (actor:Person)-[:DIRECTED]->(movie:Movie)<-[:ACTED_IN]-(actor)
RETURN actor.name AS Actor, movie.title AS Movie;
```

## 2. Para cada filme realizado depois de 2005, apresente os nomes de todos os atores que atuaram nesse filme.
```sql
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)
WHERE movie.released > 2005
RETURN movie.title AS MovieTitle, movie.released AS MovieData, collect(actor.name) AS Actors;
```

## 3. Encontre pares de nós com mais do que uma relação entre si.
```sql
MATCH (a)-[r]->(b)
WITH a, b, collect(r) AS Relations, COUNT(r) AS relCount
WHERE relCount > 1
RETURN a AS NodeA, b AS NodeB, Relations, relCount;
```

## 4. Encontre todos os pares de pessoas que fizeram revisões do mesmo filme. Apresente os seus nomes e título de cada filme.
```sql
MATCH (person1:Person)-[:REVIEWED]->(movie:Movie)<-[:REVIEWED]-(person2:Person)
WHERE person1 < person2
RETURN DISTINCT person1.name AS Reviewer1, person2.name AS Reviewer2, movie.title AS Movie;
```

## 5. Encontre todos os pares de atores que atuaram em vários filmes juntos.
```sql
MATCH (actor1:Person)-[:ACTED_IN]->(movie:Movie)<-[:ACTED_IN]-(actor2:Person)
WHERE actor1 < actor2
WITH actor1, actor2, COUNT(movie) AS MoviesTogether
WHERE MoviesTogether > 1
RETURN actor1.name AS Actor1, actor2.name AS Actor2, MoviesTogether;
```

## 6. Determine a idade média do elenco do filme "Apollo 13" no ano do lançamento do filme.
```sql
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)
WHERE movie.title = "Apollo 13"
WITH movie.released - actor.born AS Age
RETURN AVG(Age) AS AverageAges;
```

## 7. Encontre os 10 filmes com o elenco mais velho no momento do lançamento do filme. Apresente o filme e a idade média arredondada a 2 casas decimais, por ordem decrescente.
```sql
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)
WITH movie, movie.released - actor.born AS Age
RETURN round(AVG(Age), 2) AS AverageAges, movie.title AS Movie
ORDER BY AverageAges DESC
LIMIT 10;
```

## 8. Apresente o subgrafo ACTED_IN do filme com o elenco mais novo, no momento do lançamento do filme.
```sql
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)
WITH movie, movie.released - actor.born AS Age
WITH movie, round(AVG(Age), 2) AS AverageAges
ORDER BY AverageAges ASC
LIMIT 1
MATCH (actor:Person)-[r:ACTED_IN]->(movie)
RETURN actor, r, movie;
```

## 9. Qual é o caminho mais curto (usando qualquer tipo de relação) entre John Cusack e Demi Moore?
```sql
MATCH p=shortestPath(
    (actor1:Person {name:"John Cusack"})-[*]-(actor2:Person {name:"Demi Moore"})
)
RETURN p;
```

## 10. Qual a dimensão caminho mais curto (usando qualquer tipo de relação) entre Keanu Reeves e Tom Cruise?
```sql
MATCH p=shortestPath(
    (actor1:Person {name:"Keanu Reeves"})-[*]-(actor2:Person {name:"Tom Cruise"})
)
RETURN length(p) AS ShortestPath;
```

## 11. Quais são as dimensões do caminho mais curto entre pessoas com nome Jim e pessoas com nome Kevin?
```sql
MATCH (jim:Person), (kevin:Person)
WHERE jim.name STARTS WITH "Jim" AND kevin.name STARTS WITH "Kevin"
MATCH p = shortestPath((jim)-[*]-(kevin))
RETURN jim.name AS JimPerson, kevin.name AS KevinPerson, length(p) AS PathLength
ORDER BY PathLength ASC;
```

## 12. Que pessoas têm uma distância 2 para Jim Cash (a distância entre duas pessoas é o comprimento do caminho mais curto entre eles)?
```sql
MATCH (jim:Person {name:"Jim Cash"})-[*2]-(person:Person)
RETURN DISTINCT person.name AS Person;
```

## 13. Qual é a maior distância de uma pessoa para Kevin Bacon?
```sql
MATCH (kevin:Person {name: "Kevin Bacon"}), (person:Person)
WHERE person <> kevin
MATCH p = shortestPath((kevin)-[*]-(person))
RETURN person.name AS Person, length(p) AS Distance
ORDER BY Distance DESC
LIMIT 1;
```

## 14. Qual é a maior distância entre duas pessoas?
```sql
MATCH (person1:Person), (person2:Person)
WHERE person1 <> person2
MATCH p = shortestPath((person1)-[*]-(person2))
RETURN person1.name AS Person1, person2.name AS Person2, length(p) AS Distance
ORDER BY Distance DESC
LIMIT 1;
```

## 15. Qual é a distribuição de distâncias em pares (isto é, para a distância 1, 2, 3, ..., quantos pares de pessoas têm essa distância um do outro)?
```sql
MATCH (person1:Person), (person2:Person)
WHERE person1 <> person2
RETURN length(shortestPath((person1)-[*]-(person2))) AS Distance, COUNT(*) AS Pairs
ORDER BY Distance;
```

## 16. Indique as 10 pessoas com menor distância média em que o caminho entre elas são relações do tipo ACTED_IN.
```sql
MATCH path = shortestPath((actor1:Person)-[:ACTED_IN*]-(actor2:Person))
WHERE actor1 <> actor2
RETURN actor1.name AS Actor1, actor2.name AS Actor2, AVG(length(path)) AS AvgDistance
ORDER BY AvgDistance ASC
LIMIT 10;
```

## 17. Apresente os atores que tiveram pelo menos uma role para personagens com o prefixo 'Dr.'. O resultado deve ser uma lista com o par “Ator, Lista das roles”.
```sql
MATCH (actor:Person)-[r:ACTED_IN]->(movie:Movie)
WHERE ANY(role IN r.roles WHERE role STARTS WITH "Dr.")
RETURN actor.name AS Actor, collect(r.roles) AS Roles;
```

## 18. Qual é o diretor que trabalhou com o maior número de atores únicos, e quem são esses atores?
```sql
MATCH (director:Person)-[:DIRECTED]->(movie:Movie)<-[:ACTED_IN]-(actor:Person)
WITH director, COUNT(DISTINCT actor) AS ActorCount, COLLECT(DISTINCT actor.name) AS Actors
ORDER BY ActorCount DESC
LIMIT 1
RETURN director.name AS Director, ActorCount, Actors;
```

## 19. Lista os atores que atuaram em mais de 5 filmes, apresetando o nome do ator e o número de filmes.
```sql
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)
WITH actor, COUNT(movie) AS MoviesCount
WHERE MoviesCount > 5
RETURN actor.name AS Actor, MoviesCount
ORDER BY MoviesCount DESC;
```

## 20. Qual filme tem o maior número de revisões, e quais são os comentários e avaliações associadas?
```sql
MATCH (reviewer:Person)-[r:REVIEWED]->(movie:Movie)
WITH movie, COUNT(r) AS ReviewCount, COLLECT({Reviewer: reviewer.name, Summary: r.summary, Rating: r.rating}) AS Reviews
ORDER BY ReviewCount DESC
LIMIT 1
RETURN movie.title AS Movie, ReviewCount, Reviews;
```
